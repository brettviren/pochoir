#!/usr/bin/env snakemake
# -*- snakemake -*-

store="test-sandh2d-store"
plots="test-sandh2d-plots"

ENGINES = ['torch','cupy', 'numpy', 'numba']

DOMAINS = ['drift', 'width']
FIELDS = {
    'weight-ind': {
        "domain": "weight",
    },
    'weight-col': {
        "domain": "weight",
    },
    'drift': {
        "domain": "drift",
    }
}
WFIELDS = [n for n,d in FIELDS.items() if d['domain'] == 'weight']
DFIELDS = [n for n,d in FIELDS.items() if d['domain'] == 'drift']

TAXONS = ['initial', 'boundary', 'potential', 'increment']
VALUE_TAXONS = ['initial', 'potential', 'increment']

rule configs:
    input:
        "test/test-sandh2d.jsonnet"
    output:
        f'{store}/domains/drift.json',
        f'{store}/domains/weight.json'
    shell: '''
    mkdir -p {store}/domains ;
    jsonnet -m {store} {input}
    '''

rule gen:
    input:
        lambda w: '{store}/domains/{domain}.json'.format(store=store, **FIELDS[w.field])
    output:
        iva = f'{store}/{{engine}}/initial/{{field}}.npz',
        bva = f'{store}/{{engine}}/boundary/{{field}}.npz'
    shell: '''
    pochoir --store {store} \
    gen --generator sandh2d --domain {input} \
    --initial {output.iva} \
    --boundary {output.bva} \
    {store}/{wildcards.field}.json 
    '''

rule fdm:
    input:
        iva = f'{store}/{{engine}}/initial/{{field}}.npz',
        bva = f'{store}/{{engine}}/boundary/{{field}}.npz'
    output:
        pot = f'{store}/{{engine}}/potential/{{field}}.npz',
        inc = f'{store}/{{engine}}/increment/{{field}}.npz'
    params:
        nepochs = 100,
        epoch = 100,
        prec = 0.01,
        edges = "fixed,periodic"

    shell: '''
    pochoir --store {store} \
    fdm --engine {wildcards.engine} \
    --nepochs {params.nepochs} --epoch {params.epoch} \
    --precision {params.prec} --edges {params.edges} \
    --initial {input.iva} --boundary {input.bva} \
    --potential {output.pot} --increment {output.inc}
    '''

rule plots_linear:
    input:
        f'{store}/{{engine}}/{{taxon}}/{{field}}.npz'
    output:
        f'{plots}/{{engine}}/{{taxon}}/{{field}}-linear.png'
    shell: '''
    pochoir --store {store} plot-image -s linear -a {input} -o {output}
    '''

rule plots_signedlog:
    input:
        f'{store}/{{engine}}/{{taxon}}/{{field}}.npz'
    output:
        f'{plots}/{{engine}}/{{taxon}}/{{field}}-signedlog.png'
    shell: '''
    pochoir --store {store} plot-image -s signedlog -a {input} -o {output}
    '''

rule all:
    input:
        rules.configs.output,
        expand(rules.gen.output, domain=["drift"],
               field=DFIELDS, engine=ENGINES),
        expand(rules.gen.output, domain=["weight"],
               field=WFIELDS, engine=ENGINES),
        expand(rules.fdm.output, field=FIELDS, engine=ENGINES),
        expand(rules.plots_linear.output,
               field=FIELDS, engine=ENGINES, taxon=TAXONS),
        expand(rules.plots_signedlog.output,
               field=FIELDS, engine=ENGINES, taxon=VALUE_TAXONS)

